/**
 * Script to include kotlin
 */
buildscript {
    ext.kotlin_version = '1.0.2'
    ext.dokka_version = '0.9.9'
    ext.spek_version = '1.0.25'
    ext.spring_version = '1.4.0.RELEASE'
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"

        classpath "net.ltgt.gradle:gradle-apt-plugin:0.6"
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.2'
    }
}

allprojects {
    buildDir = new File(rootProject.projectDir, "build/" + project.name)

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "http://repository.jetbrains.com/all" }
    }
}

subprojects {
    if (project.name.startsWith("linkbait-plugins/")) {
        task assemble {
            doLast {
                copy {
                    from "$project.buildDir/libs"
                    into "$rootProject.buildDir/plugins/"
                    include '**/*.jar'
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

import com.bmuschko.gradle.docker.tasks.image.Dockerfile

import java.nio.charset.Charset

mainClassName = 'com.nishtahir.linkbait.Application'

version = getVersion()

task copyDockerArchiveResources(type: Copy) {
    group "docker"
    dependsOn distTar
    from { distTar.archivePath }
    into { createDockerfile.destFile.parentFile }
}

task createDockerfile(type: Dockerfile) {
    group "docker"
    dependsOn copyDockerArchiveResources
    destFile = project.file("$project.buildDir/docker/Dockerfile")
    from 'nishtahir/openjfx:8-jre'
    maintainer 'Nish Tahir <nishtahir@outlook.com>'
    addFile({ distTar.archivePath.name }, { '/' })
    entryPoint { getEntryPoint() }
    exposePort 8080
}

def getEntryPoint() {
    String installDir = distTar.archiveName - ".${distTar.extension}"
    "/$installDir/bin/$project.applicationName".toString()
}

def getVersion() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    def matcher = stdout.toString().trim() =~ /^(\d+)\.(\d+)-(\d+)-g([a-zA-z0-9]+)$/
    if (matcher.matches()) {
        def major = matcher.group(1)
        def minor = matcher.group(2)
        def patch = matcher.group(3)
        def preRelease = matcher.group(4)
        return "${major}.${minor}.${patch}-${preRelease}"
    }
}

task printVersion() {
    println version
}

task cacheVersion() {
    def file = file(".version")
    file.delete()
    file.write(version + "\n", "UTF-8")
}

dependencies {
    compile project(':linkbait-core')
    compile project(':linkbait-server')
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile 'com.beust:jcommander:1.48'
    compile 'com.brsanthu:google-analytics-java:1.1.2'

    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.2"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.spek:spek:$spek_version"
}
