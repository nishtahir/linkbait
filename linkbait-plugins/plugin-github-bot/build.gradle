buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        // 1.3.3 doesn"t work as expected
        classpath "com.github.jruby-gradle:jruby-gradle-plugin:1.3.3"
        classpath "com.github.jruby-gradle:jruby-gradle-jar-plugin:1.3.3"
    }
}

apply plugin: "java"
apply plugin: "com.github.jruby-gradle.base"
apply plugin: "com.github.jruby-gradle.jar"

import com.github.jrubygradle.JRubyExec

// So IDEA will actually pick it up. I like my sources organized
def rubySources = "$projectDir/src/main/ruby"
sourceSets.main.java.srcDirs += rubySources

// It would be nice for Java to build any generated classes.
// This is so the classloaded will have something to load.
def generatedSourcesRoot = "$buildDir/generated"
sourceSets.main.java.srcDirs += generatedSourcesRoot

configurations {
    compile.extendsFrom jrubyJar
    jrubyJar.extendsFrom gems
}

dependencies {
    /* Using the built-in `jrubyJar` configuration to describe the
     * dependencies our jrubyJar task will need, so the gem is properly
     * included in the resulting .jar file
     */

    gems "rubygems:colorize:0.7.7+"
    gems "rubygems:octokit:4.3.0+"
    jrubyJar "org.slf4j:slf4j-simple:1.7.21"
    jrubyJar "com.github.lalyos:jfiglet:0.0.7"
    jrubyJar project(":linkbait-plugin-api")
    jrubyJar project(":linkbait-test")

    compile group: 'org.jruby', name: 'jruby-complete', version: '9.1.5.0'

    testCompile "junit:junit:4.12"
}

task compileRuby(type: JRubyExec) {
    file(generatedSourcesRoot).mkdirs()
    jrubyArgs "-S", "jrubyc", "--java", rubySources, "-t", generatedSourcesRoot
}

compileJava.dependsOn(compileRuby)

jrubyJar {
//    //We want to use this Ruby script as our start point when the jar executes
    from sourceSets.main.output
//    initScript "${projectDir}/main.rb"

    baseName = 'GitHubBot'
    version = '1.0'

    // Looks like using closures here causes it to crash
    // https://github.com/jruby-gradle/jruby-gradle-plugin/issues/263
    manifest.attributes 'Plugin-Class': 'com.nishtahir.linkbait.github.GithubPlugin',
            'Plugin-Id': 'com.nishtahir.linkbait.github.GithubPlugin',
            'Plugin-Version': '1.0.0',
            'Plugin-Provider': 'Nish Tahir',
            'Plugin-Description': 'It took way too long to get this to work...',
            "Dependencies": collectDependencies()

}

def collectDependencies() {
    return configurations.compile.allDependencies.findAll {
        it.group != "rubygems"
    }.collect {
        "${it.group}:${it.name}:${it.version}"
    }.join(',')
}

assemble.dependsOn jrubyJar
jar.enabled = true
